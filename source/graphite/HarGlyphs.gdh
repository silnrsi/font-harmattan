////////////////////////////////////////////////////////////////////////////////////////////
//
//	File: HarGlyphs.gdh
//
//	Glyph definitions and attachment points for Harmattan
//
////////////////////////////////////////////////////////////////////////////////////////////

/*
	This file is part of Harmattan font family 
	(http://scripts.sil.org/Harmattan) and is 
	Copyright (c) 2014-2020 SIL International (http://www.sil.org/),
	with Reserved Font Names "Harmattan" and "SIL".

	This Font Software is licensed under the SIL Open Font License,
	Version 1.1.

	You should have received a copy of the license along with this Font Software.
	If this is not the case, go to (http://scripts.sil.org/OFL) for all the
	details including an FAQ.
*/

environment {AttributeOverride = true};


table(glyph)
	
/********************************************************/


//------------------------------------------------------------------------------------------
//
// Mirroring
//

// g_radical.mirror.glyph = g_radical_rtl;
// g_radical.mirror.isEncoded = 0;

// Used for Graphite-1 style mirroring:

cMirrorOpen  = (g_parenleft  g_less    g_bracketleft  g_braceleft  g_guilsinglleft  g_guillemetleft ); // g_radical);
cMirrorClose = (g_parenright g_greater g_bracketright g_braceright g_guilsinglright g_guillemetright); // g_radical_rtl);


//------------------------------------------------------------------------------------------
//
//	Contextual forms
//


// Lowered hamza

cLowHamzaComposed { component {
	base = box(0, bb.bottom, aw, bb.bottom + (bb.height*5)/8);
	hamza = box(0, bb.bottom + (bb.height*5)/8, aw, bb.top) }};
// slight adjustments to components:
g_waw_hamzaabove_ar { component {
	base.top = bb.bottom + (bb.height*3)/4;
	hamza.bottom = bb.bottom + (bb.height*3)/4 }};
g_yeh_hamzaabove_ar { component.hamza.right = aw*3/4 };
g_yeh_hamzaabove_ar_medi { component {
	base.top = bb.bottom + bb.height/2;
	hamza.bottom = bb.bottom + bb.height/2 }};
g_yeh_hamzaabove_ar_fina { component.hamza.right = aw*3/4 };

// Subtending marks

cSignTakes4 = (g_year_ar g_samvat_ar g_numbermark_ar g_pagenumber_ar); // medium

cSignTakes3Medium = (g_year_ar g_samvat_ar g_numbermark_ar g_number_ar g_pagenumber_ar);
cSignTakes3Small  = (g_endofayah_ar g_endofayah_ar_alt g_endofayah_ar_alt_b);

cSignTakes2 = (g_year_ar g_samvat_ar g_numbermark_ar g_number_ar g_pagenumber_ar g_endofayah_ar g_endofayah_ar_alt g_endofayah_ar_alt_b
	g_footnotemarker_ar); // medium

cSignTakes1 = (g_year_ar g_samvat_ar g_numbermark_ar g_number_ar g_pagenumber_ar g_endofayah_ar g_endofayah_ar_alt g_endofayah_ar_alt_b
	g_footnotemarker_ar); // medium
	
cSign4 = (g_year_ar_4 g_samvat_ar_4 g_numbermark_ar_4 g_pagenumber_ar_4);
cSign3Medium = (g_year_ar_3 g_samvat_ar_3 g_numbermark_ar_3 g_number_ar_3 g_pagenumber_ar_3);
cSign3Small  = (g_endofayah_ar_3 g_endofayah_ar_alt_3 g_endofayah_ar_alt_b_3);
cSign3 = (cSign3Medium cSign3Small);
cSign2 = (g_year_ar_2  g_samvat_ar_2 g_numbermark_ar_2 g_number_ar_2  g_pagenumber_ar_2 g_endofayah_ar_2 
	g_endofayah_ar_alt_2 g_endofayah_ar_alt_b_2 g_footnotemarker_ar_2);
cSign1 = (g_year_ar    g_samvat_ar   g_numbermark_ar   g_number_ar    g_pagenumber_ar   g_endofayah_ar    
	g_endofayah_ar_alt   g_endofayah_ar_alt_b   g_footnotemarker_ar);


cSignAll = (cSign1 cSign2 cSign3 cSign4);

cSignDigit = (cDigitsArabicMedium cDigitsArabicSmall 
			 cDigitsEasternMedium cDigitsEasternSmall
			  cDigitsLatinMedium cDigitsLatinSmall) ;



//------------------------------------------------------------------------------------------
//
//	Feature-related variants
//


cno_Heh     = (g_heh_ar           g_heh_ar_init         g_heh_ar_medi               g_heh_ar_fina);
cHehSindhi  = (g_heh_ar_knotted   g_heh_ar_init         g_heh_ar_knotted_high_medi  g_heh_ar_knotted_high_fina);
cHehUrdu    = (g_heh_ar           g_heh_ar_hooked_init  g_heh_ar_hooked_medi        g_heh_ar_hooked_fina);
cHehKurdish = (g_heh_ar_knotted   g_heh_ar_init         g_heh_ar_medi               g_heh_ar_knotted_high_fina);

cEndOfAyah        = (g_endofayah_ar      g_endofayah_ar_2      g_endofayah_ar_3);
cEndOfAyahCircle  = (g_endofayah_ar_alt  g_endofayah_ar_alt_2  g_endofayah_ar_alt_3);
cEndOfAyahSquare  = (g_endofayah_ar_alt_b g_endofayah_ar_alt_b_2 g_endofayah_ar_alt_b_3);

zeroWidthJoiner = postscript("zerowidthjoiner");
zeroWidthNonJoiner = postscript("zerowidthnonjoiner");
leftToRightMark = postscript("lefttorightmark") {dir = DIR_LEFT};
rightToLeftMark = postscript("righttoleftmark") {dir = DIR_RIGHT};
leftToRightEmbedding = postscript("lefttorightembedding") {dir = DIR_LRE};
rightToLeftEmbedding = postscript("righttoleftembedding") {dir = DIR_RLE};
popDirectionalFormatting = postscript("popdirectionalformatting") {dir = DIR_PDF};
leftToRightOverride = postscript("lefttorightoverride") {dir = DIR_LRO};
rightToLeftOverride = postscript("righttoleftoverride") {dir = DIR_RLO};
leftToRightIsolate = postscript("lefttorightisolate") {dir = DIR_LRI} ;
rightToLeftIsolate = postscript("righttoleftisolate") {dir = DIR_RLI} ;
firstStrongIsolate = postscript("firststrongisolate") {dir = DIR_FSI} ;
popDirectionalIsolate = postscript("popdirectionalisolate") {dir = DIR_PDI} ;
arabicLetterMark = postscript("mark-ar") {dir = DIR_ARABIC} ;


//------------------------------------------------------------------------------------------
//
// UTR53
//
// Not a full implementation. 
// Define groups based on combining class and whether they are Modifier Combining Marks (MCM) according to UTR53

// Desired order: cUTR53_220MCM cUTR53_230MCM cUTR53_shadda cUTR53_fixedPos cUTR53_alef cUTR53_220other cUTR53_230other
// Technically we should also sort the fixedPos but unlikely to have more than one of these

cUTR53_1      = (cUTR53_220MCM) ;
cUTR53_1_skip = (cUTR53_230MCM cUTR53_shadda cUTR53_fixedPos cUTR53_alef cUTR53_230other) ;

cUTR53_2      = (cUTR53_230MCM) ;
cUTR53_2_skip = (cUTR53_shadda cUTR53_fixedPos cUTR53_alef cUTR53_220other cUTR53_220MCM) ;

cUTR53_3      = (cUTR53_shadda) ;
cUTR53_3_skip = (cUTR53_fixedPos cUTR53_alef cUTR53_220other cUTR53_220MCM cUTR53_230other cUTR53_230MCM) ;

cUTR53_4      = (cUTR53_fixedPos) ;
cUTR53_4_skip = (cUTR53_alef cUTR53_220MCM cUTR53_230other cUTR53_230MCM) ;

cUTR53_5      = (cUTR53_alef) ;
cUTR53_5_skip = (cUTR53_220MCM cUTR53_230other cUTR53_230MCM) ;

cUTR53_6      = (cUTR53_220other cUTR53_220MCM) ;       // Not actually needed b/c attachment will handle these
cUTR53_6_skip = (cUTR53_230other cUTR53_230MCM) ;



//------------------------------------------------------------------------------------------
//
//	Marks
//

// marks that can combine with vowel marks:
cMark1Above = (cMarkGroup1A);

cMark1Below = (cMarkGroup1B);

// vowels
cMark2Above = (cMarkGroup2A cMarkGroup3A);

cMark2Below = (cMarkGroup2B);

// Maddah is different from hamza and shadda in that it goes above the vowels, but below the quranic
// marks. However, Jonathan warns that this behavior may need to change someday.
cMark3Above = (cMarkGroup4A);

// (no marks below that behave like maddah)

// quranic marks
cMark4Above = (cMarkGroup5A);

cMark4Below = (cMarkGroup3B);

cMark1 = (cMark1Above cMark1Below);
cMark2 = (cMark2Above cMark2Below);
cMark3 = (cMark3Above);
cMark4 = (cMark4Above cMark4Below);

cAnyMarks = (cMark1 cMark2 cMark3 cMark4);
cAnyMarksAbove = (cMark1Above cMark2Above cMark3Above cMark4Above);
cAnyMarksBelow = (cMark1Below cMark2Below cMark4Below);

cMark234 = (cMark2 cMark3 cMark4);
cMark34  = (cMark3 cMark4);

endtable; // glyph

endenvironment;
